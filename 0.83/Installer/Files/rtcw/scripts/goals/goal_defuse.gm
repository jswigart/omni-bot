this.Name = "DEFUSE";
this.Parent = "HighLevel";
this.SkipGetPriorityWhenActive = false; // cs: let them abort for higher priority defuses

this.LimitToWeapon(WEAPON.PLIERS);

this.Initialize = function()
{
	this.Bot.QueryGoalsTable = table();
	this.TargetEntity = null;
	this.NoGibbing = GetCvar("g_botgib") == 0;
};

this.GetPriority = function()
{
	// cs: GetPriority seems to be called the same frame as this.Finished(). need to wait a frame
	//     so all the vars can be cleaned up. this.MapGoal is not null for example if goal is
	//     aborted for a higher priority one; which causes evaluation for the new goal to happen twice
	yield();

	while(1)
	{
		count = this.QueryGoals(this.Bot.QueryGoalsTable,"DEFUSE_.*");
		if ( count > 0 )
		{
			// always go for the closest one of the highest priority
			dist = null;
			evalDist = null;
			closestGoal = null;
			priority = 0;

			foreach ( id and goal in this.Bot.QueryGoalsTable )
			{
				evalDist = this.Bot.DistanceTo(goal.GetEntity());
				evalPriority = goal.GetGoalPriority(this.Bot);

				if ( !dist || (evalDist < dist && evalPriority >= priority) ) {
					dist = evalDist;
					closestGoal = goal;
					priority = evalPriority;
				}
			}

			if ( closestGoal && closestGoal != this.MapGoal ) {
				// cs: this is temp. goals of the same type are not natively being aborted even for higher priority goals
				//     but i think that they should be.
				if ( this.MapGoal ) {
					//if (this.DebugAbortForHigherPriority) { this.Bot.Say("aborting defuse for higher priority"); }
					tableClear(this.Bot.QueryGoalsTable);
					this.Finished();
				}

				this.MapGoal = closestGoal;
				this.Priority = priority;
			}
		}

		tableClear(this.Bot.QueryGoalsTable);

		sleep(2);
	}
};

this.Enter = function()
{
	if(!this.MarkInProgress(this.MapGoal)) {
		this.BlackboardDelay(5, this.MapGoal);
		this.Finished();
	}

	this.TargetEntity = this.MapGoal.GetEntity();
};

this.Update = function()
{
	if ( this.RouteTo(this.MapGoal, 64.f) == EVENT.PATH_SUCCESS )
	{
		if(!this.MarkInUse(this.MapGoal)) {
			this.BlackboardDelay(5, this.MapGoal);
			this.Finished();
		}

		pos = GetEntPosition(this.TargetEntity);
		this.AddAimRequest(Priority.High, "position", pos);

		// when bot gib is disabled, humans are exploiting it by laying on top of dyno with /injure
		// so let's force a gib
		if (this.NoGibbing) {
			gibTarget = null;
			ent = GetEntityInSphere(this.Bot.GetPosition(), 300, CLASS.ANYPLAYER, gibTarget);
			dowhile(ent) {
				if ( ent && DistanceBetween(this.MapGoal.GetEntity(), ent) < 100 && !this.Bot.IsAllied(ent)
					&& !GetEntFlags(ent, ENTFLAG.LIMBO) ){
					gibTarget = ent;
					break;
				}
				ent = GetEntityInSphere(this.Bot.GetPosition(), 300, CLASS.ANYPLAYER, ent);
			}

			if (gibTarget) {
				this.AddWeaponRequest(Priority.High, this.Bot.GetCurrentWeapon() ); // don't reload
				this.AddAimRequest(Priority.High, "position", GetEntPosition(gibTarget));
				this.Bot.HoldButton(BTN.ATTACK1, 2);
				sleep(2);
				this.Finished(); // just reset the goal
			}
		}

		this.AddWeaponRequest(Priority.High, WEAPON.PLIERS );
		this.BlockForWeaponChange( WEAPON.PLIERS );

		while( this.TargetEntity && EntityIsValid(this.TargetEntity) ) { // goal deleted with entity
			pos = GetEntPosition(this.TargetEntity);
			if ( pos ) {
				this.AddAimRequest(Priority.High, "position", pos + Vector3(0,0,12));

				eyePos = GetEntEyePosition(this.Bot.GetGameEntity());
				heightDiff = pos.z - eyePos.z;
				if ( heightDiff > 20.f ) {
					this.Bot.PressButton(BTN.JUMP);
				}
				else {
					this.Bot.HoldButton(BTN.CROUCH, 0.5);
				}

				if ( this.Bot.DistanceTo(pos) > 64 ) {
					this.Bot.MoveTowards(pos);
				}

				this.Bot.HoldButton(BTN.ATTACK1, 0.25);
			}
			else {
				this.Finished();
			}
			sleep(0.25);
		}
	}
	else
	{
		this.BlackboardDelay(10, this.MapGoal);
		Util.ScriptDebugPrint(this.MapGoal.GetName(), "pathfailed");
	}

	this.Finished();
};

this.Exit = function()
{
	this.TargetEntity = null;
	this.Bot.ReleaseButton(BTN.CROUCH);
	this.MapGoal = null;
};
