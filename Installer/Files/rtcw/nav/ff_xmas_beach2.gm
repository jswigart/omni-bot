global Map =
{
	Quiet = true,
	Debug = false,
	DebugAvailability = false,

	Airstrike =
	{
		bridge =
		{
			Enabled = false,
			Team = ( 1 << TEAM.AXIS ),
			//Facing = Vector3(-0.774,-0.000,0.631),
			Facing = Vector3( -0.643, -0.366, 0.820 ),
			NoPause = true,
		},
	},

	Navigation =
	{
		towerjump =
		{
			navigate = function( _this )
			{
				_this.Bot.PressButton( BTN.JUMP );
				sleep( 1 );
				_this.Bot.HoldButton( BTN.STRAFE_R, 1 );
				_this.Bot.HoldButton( BTN.JUMP, 2 );
				sleep( 2 );
			},
		},

		trenchjump =
		{
			navigate = function( _this )
			{
				_this.Bot.PressButton( BTN.JUMP );
				_this.Bot.HoldButton( BTN.STRAFE_L, 0.5 );
				_this.Bot.HoldButton( BTN.SPRINT, 1 );
				sleep( 1 );
			},
		},
	},

	Checkpoint_beach_flag = "CHECKPOINT_beach_flag",
	FLAG_FF_Christmas_List = "FLAG_FF_Christmas_List",
	Cappoint_Transmitter = "CAPPOINT_Transmitter",
	//Plant_Door = "PLANT_Sea_Wall_door",
	//Plant_Wall = "PLANT_Sea_Wall_breach",
	Explode_t373 = "EXPLODE_t373",
	Explode_t374 = "EXPLODE_t374",
	Explode_t375 = "EXPLODE_t375",
	Mount_mg42_1 = "MOUNTMG42_mg42_1",
	Mount_mg42_2 = "MOUNTMG42_mg42_2",
	Mount_mg42_3 = "MOUNTMG42_mg42_3",
	Mount_t364 = "MOUNTMG42_t364",
	Mount_t367 = "MOUNTMG42_t367",
	Repair_mg42_1 = "REPAIRMG42_mg42_1",
	Repair_mg42_2 = "REPAIRMG42_mg42_2",
	Repair_mg42_3 = "REPAIRMG42_mg42_3",
	Repair_t364 = "REPAIRMG42_t364",
	Repair_t367 = "REPAIRMG42_t367",
	DontDispenseAmmo = true,

	CompSettings = false,

	//set status' for conditionals
	FlagStatus = 0, //0 nobody, 1 axis, 2 allies
	DocStatus = false, //home
	DoorStatus = false, //intact
	WallStatus = false, //intact
	WarRoomShift = false,

	BackStairsTrigger =
	{
		Name = "BackStairsTrigger",
		TriggerOnClass = CLASS.ANYPLAYER,
		OnEnter = function( ent )
		{
			if ( GetEntTeam( ent ) == TEAM.ALLIES ) {
				Map.WarRoomShift = true;

				if ( !Map.DocStatus ) {
					SetAvailableMapGoals( TEAM.AXIS, false, "DEFEND_.*" );
					SetAvailableMapGoals( TEAM.AXIS, true, "DEFEND_rWar.*" );

					if ( Map.CompSettings ) {
						RTCWUtil.SetTeamSuicide( TEAM.AXIS, 0, 0 );
					}
				} else if ( Map.DocStatus )   {
					if ( GetEntFlags( ent, ENTFLAG.CARRYINGGOAL ) ) {
						// Martin - Let bots tell teammates where documents are going
						b = Util.GetBotByName( GetEntName( ent ) );
						if ( b ) {
							b.SayTeam( "I'm going long!" );
						}

						Map.WarRoomShift = false;
						SetGoalPriority( "DEFEND_rTransmit[123456]", 0.6 );
						SetGoalPriority( "ATTACK_bTransmit[4567]", 0.6 );   //Allies focus on one side
						SetAvailableMapGoals( TEAM.AXIS, false, "DEFEND_.*" );
						SetAvailableMapGoals( TEAM.AXIS, true, "DEFEND_rTransmit.*" );

						foreach( id and bot in BotTable )
						{
							if ( bot.GetTeam() == TEAM.AXIS ) {
								bot.SayTeam( "Docs are going long!" );
								break;
							}
						}
					}
				}
			}
		},
		OnExit = function( ent )
		{
		},
	},

	RadioStairsTrigger =
	{
		Name = "RadioStairsTrigger",
		TriggerOnClass = CLASS.ANYPLAYER,
		OnEnter = function( ent )
		{
			if ( GetEntTeam( ent ) == TEAM.ALLIES ) {
				Map.AlliesNearDocs = true;

				if ( !Map.DocStatus ) {
					Map.WarRoomShift = true;
					SetAvailableMapGoals( TEAM.AXIS, false, "DEFEND_.*" );
					SetAvailableMapGoals( TEAM.AXIS, true, "DEFEND_rWar.*" );

					if ( Map.CompSettings ) {
						RTCWUtil.SetTeamSuicide( TEAM.AXIS, 0, 0 );
					}
				} else if ( Map.DocStatus )   {
					if ( GetEntFlags( ent, ENTFLAG.CARRYINGGOAL ) ) {
						// Martin - Let bots tell teammates where documents are going
						b = Util.GetBotByName( GetEntName( ent ) );
						if ( b ) {
							b.SayTeam( "I'm going short!" );
						}

						Map.WarRoomShift = false;
						SetGoalPriority( "DEFEND_rTransmit[789]", 0.6 );
						SetGoalPriority( "DEFEND_rTransmit10", 0.6 );
						SetGoalPriority( "ATTACK_bTransmit[1236]", 0.6 );   //Allies focus on one side
						SetAvailableMapGoals( TEAM.AXIS, false, "DEFEND_.*" );
						SetAvailableMapGoals( TEAM.AXIS, true, "DEFEND_rTransmit.*" );

						foreach( id and bot in BotTable )
						{
							if ( bot.GetTeam() == TEAM.AXIS ) {
								bot.SayTeam( "Docs are going short!" );
								break;
							}
						}
					}
				}
			}
		},
		OnExit = function( ent )
		{
		},
	},

	allied_obj1_1_Destroyed = function( trigger )
	{
		if ( TestMap ) {
			return;
		}

		Map.DoorStatus = true;

		if ( !Map.DocStatus ) {
			Map.eval_beach_shift();
		}

		Map.DontDispenseAmmo = false;
		Util.MapDebugPrint( "allied_obj1_1_Destroyed" );
	},

	allied_obj1_2_Destroyed = function( trigger )
	{
		if ( TestMap ) {
			return;
		}

		Map.WallStatus = true;

		if ( !Map.DocStatus ) {
			Map.eval_beach_shift();
		}

		Map.DontDispenseAmmo = false;
		Util.MapDebugPrint( "allied_obj1_2_Destroyed" );
	},

	eval_beach_shift = function()
	{
		if ( Map.WallStatus && Map.DoorStatus ) { //both destroyed
			SetAvailableMapGoals( TEAM.AXIS, true, "DEFEND_rUpper.*" );
			SetAvailableMapGoals( TEAM.AXIS, true, "DEFEND_rAlt.*" );
			SetAvailableMapGoals( TEAM.AXIS, true, "PANZER_rUpper.*" );
			SetAvailableMapGoals( TEAM.AXIS, true, "VENOM_rUpper.*" );
			SetAvailableMapGoals( TEAM.AXIS, true, "FLAME_rUpper.*" );

			SetAvailableMapGoals( TEAM.AXIS, false, "SNIPE_rBeach.*" );
			SetAvailableMapGoals( TEAM.ALLIES, false, "SNIPE_bBeach.*" );
			SetAvailableMapGoals( TEAM.AXIS, false, "DEFEND_rWall.*" );
			SetAvailableMapGoals( TEAM.AXIS, false, "DEFEND_rDoor.*" );
			SetAvailableMapGoals( TEAM.AXIS, false, Map.Mount_t367 );
			SetAvailableMapGoals( TEAM.AXIS, false, Map.Mount_mg42_2 );
			SetAvailableMapGoals( TEAM.AXIS, false, Map.Mount_mg42_1 );

			//only axis soldiers should try and grab flag
			RTCWUtil.LimitToClass( Map.Checkpoint_beach_flag, TEAM.AXIS, CLASS.SOLDIER );

			//no more a/s here
			SetAvailableMapGoals( TEAM.ALLIES, false, "AIRSTRIKE_.*" );
			SetAvailableMapGoals( TEAM.AXIS, false, "AIRSTRIKE_.*" );
		} else if ( Map.WallStatus && !Map.DoorStatus )   { //wall destroyed, door intact
			SetAvailableMapGoals( TEAM.AXIS, true, "DEFEND_rWall.*" );
			SetAvailableMapGoals( TEAM.AXIS, true, Map.Mount_t367 );

			//shift a/s focus
			SetAvailableMapGoals( TEAM.AXIS, false, "AIRSTRIKE_.*" );
			SetAvailableMapGoals( TEAM.ALLIES, false, "AIRSTRIKE_.*" );
			SetAvailableMapGoals( TEAM.ALLIES, true, "AIRSTRIKE_blue_as3_p1" );
			SetAvailableMapGoals( TEAM.AXIS, true, "AIRSTRIKE_red_as1_p1" );
			SetAvailableMapGoals( TEAM.AXIS, true, "AIRSTRIKE_red_as3_p1" );
		} else if ( !Map.WallStatus && Map.DoorStatus )   { //door destroyed, wall intact
			SetAvailableMapGoals( TEAM.AXIS, true, "DEFEND_rDoor.*" );
			SetAvailableMapGoals( TEAM.AXIS, true, Map.Mount_mg42_2 );
			SetAvailableMapGoals( TEAM.AXIS, true, Map.Mount_mg42_1 );

			//shift a/s focus`
			SetAvailableMapGoals( TEAM.AXIS, false, "AIRSTRIKE_.*" );
			SetAvailableMapGoals( TEAM.ALLIES, false, "AIRSTRIKE_.*" );
			SetAvailableMapGoals( TEAM.AXIS, true, "AIRSTRIKE_red_as2_p1" );
			SetAvailableMapGoals( TEAM.ALLIES, true, "AIRSTRIKE_blue_as1_p1" );
			SetAvailableMapGoals( TEAM.ALLIES, true, "AIRSTRIKE_blue_as2_p1" );
		}

		//switch from sniper to other soldier weap
		if ( Map.CompSettings ) {
			RTCWUtil.SwitchWeapon( WEAPON.PANZERFAUST );
		} else
		{
			RTCWUtil.StopSniping();
		}

		//set these if either has been destroyed
//		Util.EnableGoal( Map.Checkpoint_beach_flag );
		SetAvailableMapGoals( TEAM.AXIS, false, Map.Mount_mg42_3 );
		SetAvailableMapGoals( TEAM.AXIS, false, Map.Mount_t364 );
		SetAvailableMapGoals( TEAM.AXIS, false, "DEFEND_rBeach.*" );
		SetAvailableMapGoals( TEAM.ALLIES, false, "ATTACK_bBeach.*" );

		//Wp.SetWaypointFlag( "ladder", "closed", false );	// Open up the ladder
		Util.MapDebugPrint( "eval_beach_shift" );
	},

	Allies_Capture_Flag = function( trigger )
	{
		if ( TestMap ) {
			return;
		}

		Map.FlagStatus = 2;

		SetAvailableMapGoals( TEAM.ALLIES, true, Map.FLAG_FF_Christmas_List );
		SetAvailableMapGoals( TEAM.ALLIES, false, "AIRSTRIKE_.*" );

		Map.EvalArty();

		if ( Map.DocStatus == false ) {
			SetAvailableMapGoals( TEAM.AXIS, false, "AIRSTRIKE_.*" );
			SetAvailableMapGoals( TEAM.AXIS, true, "AIRSTRIKE_rTrenchAS_.*" );
		}
		Map.Airstrike.bridge.Enabled = true;
		SetGoalPriority( "CALL.*", 0.6 );
		SetGoalPriority( "ARTILLERY.*", 0.6 );

		Util.SetMaxUsersInProgress( 1, "CHECKPOINT.*" );    // Martin - 1 axis at a time go for it
		Wp.SetWaypointFlag( "pt_nav_towerjump", "closed", false );  // Open up the towerjump
		Util.MapDebugPrint( "Allies_Capture_Flag" );
		RTCWUtil.StopSniping();
	},

	Axis_Capture_Flag = function( trigger )
	{
		if ( TestMap ) {
			return;
		}

		Map.FlagStatus = 1;
		SetAvailableMapGoals( TEAM.AXIS, false, "AIRSTRIKE_.*" );
		Map.EvalArty();

		Map.Airstrike.bridge.Enabled = false;
		SetGoalPriority( "CALL.*", 1.2 );
		SetGoalPriority( "ARTILLERY.*", 1.2 );

		Util.SetMaxUsersInProgress( 3, "CHECKPOINT.*" );    // Martin - But let 3 allies go for it.
		Util.MapDebugPrint( "Axis_Capture_Flag" );
	},

	documents_stolen = function( trigger )
	{
		if ( TestMap ) {
			return;
		}

		// cs: this gets called when picked up off ground too, so don't re-shift to war room in that case.
		if ( !Map.DocStatus ) {
			Map.DocStatus = true;

			Map.EvalArty(); //Martin - Lets stop spamming sooner

			// make sure they fully shift even if only one dyno goal is destroyed
			if ( Map.CompSettings ) {
				RTCWUtil.SwitchWeapon( WEAPON.PANZERFAUST );
			} else
			{
				RTCWUtil.StopSniping();
			}

			SetAvailableMapGoals( TEAM.AXIS, false, "SNIPE_rBeach.*" );
			SetAvailableMapGoals( TEAM.ALLIES, false, "SNIPE_bBeach.*" );
			SetAvailableMapGoals( TEAM.AXIS, false, "MOUNT.*" );
			SetAvailableMapGoals( TEAM.AXIS, false, "PANZER_rUpper.*" );
			SetAvailableMapGoals( TEAM.AXIS, false, "VENOM_rUpper.*" );
			SetAvailableMapGoals( TEAM.AXIS, false, "FLAME_rUpper.*" );


			//SetAvailableMapGoals( TEAM.ALLIES, true, "ATTACK_bTransmit.*" );
			SetAvailableMapGoals( TEAM.ALLIES, true, "ATTACK_bBeach.*" );
			SetAvailableMapGoals( TEAM.ALLIES, true, "ATTACK_bUpper.*" );
			SetAvailableMapGoals( TEAM.AXIS, false, Map.Checkpoint_beach_flag );
			SetAvailableMapGoals( TEAM.AXIS, false, "DEFEND.*" );
			SetAvailableMapGoals( TEAM.AXIS, true, "DEFEND_rWar.*" ); //make sure
			SetAvailableMapGoals( TEAM.AXIS, false, "AIRSTRIKE_.*" );

			// cs: just exit right away if docs are returned
			timeout = 0;
			while ( timeout <= 8 && Map.DocStatus == true )
			{
				timeout += 1;
				sleep( 1 );
			}

			// cs: moved this out. in both cases we want them out of war room ...
			SetAvailableMapGoals( TEAM.AXIS, false, "DEFEND_rWar.*" );

			// cs: changed this. why would they defend trasmit when docs are returned?
			if ( Map.DocStatus ) {
				SetAvailableMapGoals( TEAM.AXIS, true, "DEFEND_rTransmit.*" );


				if ( Map.CompSettings ) {
					RTCWUtil.SetTeamSuicide( TEAM.AXIS, 0, 0 );
				}
			}
		}

		Util.MapDebugPrint( "documents_stolen" );

	},

	EvalArty = function()
	{
		if ( !Map.DocStatus && Map.FlagStatus == 1 ) {
			SetAvailableMapGoals( TEAM.AXIS, true, "CALLARTILLERY_ubase_spot_axis_[2]" );
			SetAvailableMapGoals( TEAM.AXIS, true, "ARTILLERY_S_rArty.*" );
		}

		if ( ( !Map.WallStatus || !Map.DoorStatus ) && !Map.DocStatus && Map.FlagStatus == 1 ) {
			SetAvailableMapGoals( TEAM.ALLIES, true, "ARTILLERY_S_bBeach.*" );
			SetAvailableMapGoals( TEAM.AXIS, false, "ARTILLERY_S_rFlag.*" );
		}

		if ( !Map.DocStatus && Map.FlagStatus == 2 ) {
			SetAvailableMapGoals( TEAM.ALLIES, true, "ARTILLERY_S_bTower.*" );
			//SetAvailableMapGoals( TEAM.ALLIES, true, "CALLARTILLERY_trenches_spot_allies" );	//Martin - This should be paththrough
			SetAvailableMapGoals( TEAM.ALLIES, false, "CALLARTILLERY_spot_beach_[12345678]" );
			SetAvailableMapGoals( TEAM.ALLIES, false, "ARTILLERY_S_bBeach.*" );
			SetAvailableMapGoals( TEAM.AXIS, true, "CALLARTILLERY_ubase_spot_axis_[2]" );
			SetAvailableMapGoals( TEAM.AXIS, true, "ARTILLERY_S_rFlag.*" );
			SetAvailableMapGoals( TEAM.AXIS, false, "ARTILLERY_S_rBeach.*" );
		}

		if ( Map.DocStatus ) {
			Util.DisableGoal( "CALLARTILLERY.*" );
			Util.DisableGoal( "ARTILLERY.*" );
		}

		Util.MapDebugPrint( "EvalArty" );
	},

	documents_returned = function( trigger )
	{
		if ( TestMap ) {
			return;
		}

		Map.DocStatus = false;

		SetAvailableMapGoals( TEAM.AXIS, true, "DEFEND_rUpper.*" );
		SetAvailableMapGoals( TEAM.AXIS, true, "DEFEND_rAlt.*" );
		//SetAvailableMapGoals( TEAM.ALLIES, false, "ATTACK_bTransmit.*" );
		SetAvailableMapGoals( TEAM.ALLIES, false, "ATTACK_bBeach.*" );
		SetAvailableMapGoals( TEAM.ALLIES, false, "ATTACK_bUpper.*" );
		SetAvailableMapGoals( TEAM.AXIS, false, "DEFEND_rTransmit.*" );
		SetAvailableMapGoals( TEAM.AXIS, true, "PANZER_rUpper.*" );
		SetAvailableMapGoals( TEAM.AXIS, true, "VENOM_rUpper.*" );
		SetAvailableMapGoals( TEAM.AXIS, true, "FLAME_rUpper.*" );

		//set these back to default ( changed in the stairs triggers )
		SetGoalPriority( "DEFEND_rTransmit.*", 0.5 );
		//if(Map.FlagStatus == 2)
		//{
		SetAvailableMapGoals( TEAM.AXIS, false, "AIRSTRIKE_.*" );
		SetAvailableMapGoals( TEAM.AXIS, true, "AIRSTRIKE_rTrenchAS_.*" );
		//}

		if ( Map.CompSettings ) {
			RTCWUtil.SetTeamSuicide( TEAM.AXIS, 1, 1 );
		}

		Map.EvalArty();
		Util.MapDebugPrint( "documents_returned" );
	},

	two_minute = function( trigger )
	{
		if ( TestMap ) {
			return;
		}

		SetAvailableMapGoals( TEAM.ALLIES, true, Map.FLAG_FF_Christmas_List );
		Util.MapDebugPrint( "two_minute" );
	},

	TowerTrigger =
	{
		Name = "TowerTrigger",
		TriggerOnClass = CLASS.ANYPLAYER,
		OnEnter = function( ent )
		{
			if ( TestMap ) {
				return;
			}

			if ( GetEntTeam( ent ) == TEAM.ALLIES ) {
				SetAvailableMapGoals( TEAM.ALLIES, false, "ARTILLERY_S_bTower.*" );
			}
		},
		OnExit = function( ent )
		{
			if ( TestMap ) {
				return;
			}

			if ( Map.DocStatus == true ) { //Docs are stolen
				return;
			}

			if ( Map.FlagStatus == 1 ) {  //Axis still own flag
				return;
			}

			if ( GetEntTeam( ent ) == TEAM.ALLIES ) {
				SetAvailableMapGoals( TEAM.ALLIES, true, "ARTILLERY_S_bTower.*" );
			}
		},
	},
};

global OnMapLoad = function()
{
	if ( TestMapOn ) {
		Util.AutoTestMap();
	}

	//OnTrigger( "Allies capture the Forward Bunker!", Map.Allies_Capture_Flag );
	//OnTrigger( "Axis reclaims the Forward Bunker!", Map.Axis_Capture_Flag );
	OnTrigger( "Allies have stolen the =FF= Christmas List!", Map.documents_stolen );
	OnTrigger( "Axis have returned the =FF= Christmas List!", Map.documents_returned );
	//OnTrigger( "Allies transmitted the =FF= Christmas List!", Map.documents_secured );
	OnTrigger( "two minute warning.", Map.two_minute );

	//some triggers to help bots know when to aggressively defend warroom
	tOne = OnTriggerRegion( Vector3( 3695,1483,896 ), 200, Map.BackStairsTrigger );
	tTwo = OnTriggerRegion( Vector3( 3614,3097,576 ), 200, Map.RadioStairsTrigger );

	tTower = OnTriggerRegion( AABB( 2399,2431,1097,3264,2880,1225 ), Map.TowerTrigger );
	//Martin - Traceboxes instead of vector
	//tLongOne = OnTriggerRegion(AABB(3935.000,1660.000,671.000,4016.000,1699.000,780.000), Map.BackStairsTrigger);
	//tShortOne = OnTriggerRegion(AABB(3516.000,2953.000,511.000,3554.000,3078.000,604.000), Map.RadioStairsTrigger);

	SetMapGoalProperties( "ATTACK_.*", {mincamptime = 15, maxcamptime = 30} );
	SetMapGoalProperties( "DEFEND_.*", {mincamptime = 20, maxcamptime = 40} );
	SetMapGoalProperties( "ATTACK_bTransmit.*", {mincamptime = 5, maxcamptime = 10} );
	SetMapGoalProperties( "DEFEND_rTransmit.*", {mincamptime = 5, maxcamptime = 10} );
	SetMapGoalProperties( "PANZER_.*", {mincamptime = 30, maxcamptime = 60} );
	SetMapGoalProperties( "FLAME_.*", {mincamptime = 30, maxcamptime = 60} );
	SetMapGoalProperties( "VENOM_.*", {mincamptime = 30, maxcamptime = 60} );
	SetMapGoalProperties( "MOUNT_.*", {mincamptime = 45, maxcamptime = 60} );
	SetMapGoalProperties( "SNIPE_.*", {mincamptime = 60, maxcamptime = 90} );
	RTCWUtil.SetPrimaryGoals( 1.0 );

	SetGoalPriority( "CALL.*", 1.2 );
	SetGoalPriority( "ARTILLERY.*", 1.2 );
	SetGoalPriority( "CHECKPOINT.*", 0.9 );
	SetGoalPriority( "DEFEND_rAlt.*", 0.45 );
	SetGoalPriority( "ATTACK_bUpper.*", 0.55 );
	SetGoalPriority( "AIRSTRIKE_.*", 0.95 );
	SetGoalPriority( "GRENADE_.*", 0.91 );

	RTCWUtil.LimitToClass( "MOUNT.*", 0, CLASS.SOLDIER, CLASS.LIEUTENANT );
	RTCWUtil.LimitToClass( "GRENADE.*", 0, CLASS.MEDIC, CLASS.LIEUTENANT );

	Util.DisableGoal( ".*", true ); //all but routes
	Util.EnableGoal( "CAPPOINT_transmitter" );
	//SetAvailableMapGoals( TEAM.ALLIES, true, "ATTACK_bBeach.*" );
	//SetAvailableMapGoals( TEAM.ALLIES, true, "SNIPE_bBeach.*" );
	SetAvailableMapGoals( TEAM.ALLIES, true, "FLAG.*" );
	SetAvailableMapGoals( TEAM.ALLIES, true, "GRENADE_.*" );

	SetAvailableMapGoals( TEAM.AXIS, true, Map.Mount_mg42_3 );
	SetAvailableMapGoals( TEAM.AXIS, true, Map.Mount_t367 );
	//SetAvailableMapGoals( TEAM.AXIS, true, "DEFEND_rBeach.*" );
	//SetAvailableMapGoals( TEAM.AXIS, true, "SNIPE_rBeach.*" );
	SetAvailableMapGoals( TEAM.AXIS, true, "DEFEND_rUpper.*" );
	SetAvailableMapGoals( TEAM.AXIS, true, "DEFEND_rAlt.*" );
	SetAvailableMapGoals( TEAM.AXIS, true, "PANZER_rUpper.*" );
	SetAvailableMapGoals( TEAM.AXIS, true, "VENOM_rUpper.*" );
	//SetAvailableMapGoals( TEAM.AXIS, true, "FLAME_rUpper.*" );
	SetAvailableMapGoals( TEAM.AXIS, true, "AIRSTRIKE_rTrenchAS_.*" );
	Map.Airstrike.bridge.Enabled = true;

	// *** Artillery
	//SetAvailableMapGoals( TEAM.AXIS, true, "CALLARTILLERY_ubase_spot_axis" );
	//SetAvailableMapGoals( TEAM.AXIS, true, "CALLARTILLERY_spot_steps" );

	// Now some targets...
	//SetAvailableMapGoals( TEAM.ALLIES, true, "ARTILLERY_S_bBeach.*" );
	SetAvailableMapGoals( TEAM.ALLIES, true, "ARTILLERY_S_bTower.*" );
	SetAvailableMapGoals( TEAM.AXIS, true, "ARTILLERY_S_rArty.*" );

	RTCWUtil.StopSniping();
	// *** End Arty

	//Wp.SetWaypointFlag( "ladder", "closed", true );	// close up the ladder
	//Wp.SetWaypointFlag( "pt_nav_towerjump", "closed", true );	// close up the towerjump

	Util.SetMaxUsersInProgress( 1, "MOUNT.*" );
	Util.SetMaxUsersInProgress( 1, "SNIPE.*" );
	Util.SetMaxUsersInProgress( 1, "ATTACK_bUpper.*" );

	MapRoutes =
	{
		FLAG_FF_Christmas_List =
		{
			ROUTE_beachspawn =
			{
				ROUTE_forwardbunker = {
					ROUTE_lbunker =
					{
						ROUTE_ubunker =
						{
							ROUTE_radio = {},
							ROUTE_ubase_stairs =
							{
								ROUTE_back_stairs =
								{
									ROUTE_duct1 =
									{
										ROUTE_duct2 = {},
										ROUTE_duct3 = {},
									},
									ROUTE_back_stairs2 = { Weight = 3, },
								},
							},
						},
					},
				},
				ROUTE_ubase =
				{
					ROUTE_back_stairs =
					{
						ROUTE_duct1 =
						{
							ROUTE_duct2 = {},
							ROUTE_duct3 = {},
						},
						ROUTE_back_stairs2 = { Weight = 3, },
					},
				},
				ROUTE_ubase_stairs =
				{
					ROUTE_back_stairs =
					{
						ROUTE_duct1 =
						{
							ROUTE_duct2 = {},
							ROUTE_duct3 = {},
						},
						ROUTE_back_stairs2 = { Weight = 3, },
					},
				},
			},

		},
		CAPPOINT_Transmitter =
		{
			ROUTE_docgrab =
			{
				//ROUTE_radio = {ROUTE_roof = {},},
				ROUTE_back_stairs = {},
			},
		},
		ATTACK_bTransmit1 =
		{
			ROUTE_beachspawn =
			{
				ROUTE_lbunker =
				{
					ROUTE_ubunker =
					{
						ROUTE_radio = {},
						ROUTE_ubase_stairs = {},
					},
				},
				ROUTE_ubase =
				{
					ROUTE_back_stairs = {},
				},
				ROUTE_ubase_stairs = {},
			},
			ROUTE_docgrab =
			{
				ROUTE_radio = {},
				ROUTE_back_stairs = { ROUTE_back_stairs3 = {}, },
			},
		},
		CHECKPOINT_beach_flag =
		{
			ROUTE_rspawn =
			{
				ROUTE_reclaim1 = {ROUTE_reclaim2 = {},},
			},
		},
	};

	//MapRoutes.FLAG_FF_Christmas_List.ROUTE_beachspawn = MapRoutes.FLAG_FF_Christmas_List.ROUTE_bunkerspawn;
	MapRoutes.ATTACK_bTransmit2 = MapRoutes.ATTACK_bTransmit1;
	MapRoutes.ATTACK_bTransmit3 = MapRoutes.ATTACK_bTransmit1;
	MapRoutes.ATTACK_bTransmit4 = MapRoutes.ATTACK_bTransmit1;
	MapRoutes.ATTACK_bTransmit5 = MapRoutes.ATTACK_bTransmit1;
	MapRoutes.ATTACK_bTransmit6 = MapRoutes.ATTACK_bTransmit1;
	MapRoutes.ATTACK_bTransmit7 = MapRoutes.ATTACK_bTransmit1;

	Util.Routes( MapRoutes );
	yield(); yield();

	if ( Map.CompSettings ) {
		RTCWUtil.SetTeamSuicide( TEAM.AXIS, 1, 1 );
	}

	Util.MapDebugPrint( "OnMapLoad" );
};

global OnBotJoin = function( bot )
{
	bot.MaxViewDistance = 3500;
	bot.TargetBreakableDist = 80.0;

	//RTCWUtil.SelectWeapon(bot, WEAPON.MAUSER);
	RTCWUtil.StopSniping();

	//default spawn
	bot.ChangeSpawnPoint( 0 );
};
