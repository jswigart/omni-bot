; Script generated by the HM NIS Edit Script Wizard.
SetCompressor lzma

; Game support
;!define ET    1
!define RTCW  1
;!define FF    1
;!define Q4    1
;!define D3    1
;!define TF2   1

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Omni-Bot"
!define PRODUCT_VERSION "0.71 STABLE"
!define PRODUCT_PUBLISHER "Omni-Bot Development Team"
!define PRODUCT_WEB_SITE "http://www.omni-bot.com"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define OMNI_BOT_ROOT_KEY "Software\Omni-bot"
; ---------------------------------------------------------------
; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME

; Components page
!insertmacro MUI_PAGE_COMPONENTS

; Directory page
!insertmacro MUI_PAGE_DIRECTORY

; Instfiles page
!insertmacro MUI_PAGE_INSTFILES

; Finish page
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

; MUI end ------
; ---------------------------------------------------------------
!include "Omni-Bot Utility.nsh"
;!include "Omni-Bot Installer Docs.nsh"
!include "Omni-Bot Installer Global Scripts.nsh"
; ---------------------------------------------------------------
Var GAME_DIR
Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
InstallDir "$PROGRAMFILES\Omni-Bot\"
ShowInstDetails show
ShowUnInstDetails show
; ---------------------------------------------------------------
!ifdef ET
      !include "Omni-Bot Installer ET.nsh"
!else ifdef RTCW
      !include "Omni-Bot Installer RTCW.nsh"
!else ifdef FF
      !include "Omni-Bot Installer FF.nsh"
!else ifdef Q4
      !include "Omni-Bot Installer Q4.nsh"
!else ifdef D3
      !include "Omni-Bot Installer D3.nsh"
!else ifdef TF2
      !include "Omni-Bot Installer TF2.nsh"
!endif
OutFile "..\Omni-Bot Installer ${GAME_ABBREV} (Full) ${PRODUCT_VERSION}.exe"
; ---------------------------------------------------------------
Function .onInit
  Call InitInstallBot
FunctionEnd
; ---------------------------------------------------------------
;Section "Documentation" SEC01
;  Call InstallDocs
;SectionEnd
; ---------------------------------------------------------------
Section "Mod support" SEC02
  Call InstallGlobalScripts
  Call InstallBot
SectionEnd
; ---------------------------------------------------------------
Section -AdditionalIcons
  SetOutPath $INSTDIR
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\Omni-Bot\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\Omni-Bot\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"

  ; add the installation path to the system path
  Push $INSTDIR
  Call AddToPath
SectionEnd

; Section descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
;  !insertmacro MUI_DESCRIPTION_TEXT ${SEC01} "Offline Documentation"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC02} "Omni-Bot support for ${GAME_ABBREV}"
!insertmacro MUI_FUNCTION_DESCRIPTION_END

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort

  Call un.InitInstallBot
FunctionEnd

Section Uninstall
  ; remove the installation path from the path
  Push $INSTDIR
  Call un.RemoveFromPath

  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"
  
  Delete "$SMPROGRAMS\Omni-Bot\Uninstall.lnk"
  Delete "$SMPROGRAMS\Omni-Bot\Website.lnk"

  RMDir "$SMPROGRAMS\Omni-Bot"

  Call un.InstallGlobalScripts
  ;Call un.InstallDocs

  Call un.InstallBot
  
  ; finally remove the installation directory
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"

  SetAutoClose true
SectionEnd
