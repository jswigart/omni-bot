//script created by palota
global Map =
{
	ShowMovers = false,
	Movers = { "MOVER_tank", "MOVER_truck" }, //truck is boat

	phase = 0,
	barrier1Destroyed = true,
	boatBarrier1Destroyed = true,

	EscortVehicle =
	{
		Tank =
		{
			Enabled = false,
			EscortVehicleGoalName = "MOVER_tank",
			LimitBots = 5,
		},
		Boat =
		{
			Enabled = false,
			EscortVehicleGoalName = "MOVER_truck",
			LimitBots = 3,
			ButtonPress = BTN.JUMP,
		},
	},

	SetSpawn1 = function(bot)
	{
		if(bot.GetTeam()==TEAM.ALLIES){
			if(Map.phase < 10 || Map.phase >= 20 && Map.phase < 40){
				bot.ChangeSpawnPoint(5); //bar
			}else if(Map.phase >= 10 && Map.phase < 15){
				bot.ChangeSpawnPoint(1); //depot
			}else if(RandInt(0,1)==0){
				bot.ChangeSpawnPoint(5);
			}else{
				bot.ChangeSpawnPoint(1);
			}
		}else{
			if(Map.phase < 10){
				bot.ChangeSpawnPoint(1); //depot bunker
			}else if(Map.phase < 20 || Map.phase >= 40){
				bot.ChangeSpawnPoint(4); //port
			}else if(RandInt(0,1)==0){
				bot.ChangeSpawnPoint(3); //axis spawn
			}else{
				bot.ChangeSpawnPoint(4);
			}
		}
	},

	SetSpawn = function()
	{
		foreach(bot in BotTable)
		{
			Map.SetSpawn1(bot);
		}
	},

	Bar_Captured = function(trigger)
	{
		SetAvailableMapGoals(TEAM.AXIS, false, "DEFEND_beach.*");
		SetAvailableMapGoals(TEAM.AXIS, true, "CHECKPOINT_rien_du_tout");
		SetAvailableMapGoals(TEAM.ALLIES, true, "PLANT_Side_Door__bunker");
		Map.SetSpawn();
		Util.MapDebugPrint("Bar_Captured");
	},

	Bar_Reclaimed = function(trigger)
	{
		SetAvailableMapGoals(TEAM.AXIS, false, "CHECKPOINT_rien_du_tout");
		Util.MapDebugPrint("Bar_Reclaimed");
	},

	Port_Captured = function(trigger)
	{
		SetAvailableMapGoals(TEAM.ALLIES, true, "CHECKPOINT_rien_du_tout_1");
		Util.MapDebugPrint("Port_Captured");
	},

	Port_Reclaimed = function(trigger)
	{
		SetAvailableMapGoals(TEAM.ALLIES, false, "CHECKPOINT_rien_du_tout_1");
		Util.MapDebugPrint("Port_Reclaimed");
	},

	Tank_Barrier1_Constructed = function(trigger)
	{
		Map.barrier1Destroyed = false;
		Util.MapDebugPrint("Tank_Barrier1_Constructed");
	},

	Tank_Barrier1_Destroyed = function(trigger)
	{
		Map.barrier1Destroyed = true;
		Util.MapDebugPrint("Tank_Barrier1_Destroyed");
	},

	Tank_Depot_Door_Destroyed = function(trigger)
	{
	 	Map.EscortVehicle.Tank.Enabled = true;
	 	SetAvailableMapGoals(TEAM.ALLIES, true, "BUILD_Tank");
	 	SetAvailableMapGoals(TEAM.ALLIES, true, "MOVER_tank");
		Util.MapDebugPrint("Tank_Depot_Door_Destroyed");
	},

	Side_Door__bunker_Destroyed = function(trigger)
	{
	 	ETUtil.EnableGoal("ROUTE_side_door");
	 	SetAvailableMapGoals(TEAM.AXIS, false, "DEFEND_side_door.*");
		Util.MapDebugPrint("Side_Door__bunker_Destroyed");
	},

	Tank_Stolen = function(trigger)
	{
		Map.phase = 10;
		Map.SetSpawn();
		Util.SetMaxUsersInProgress(2, "CHECKPOINT_rien_du_tout_1");
	 	SetAvailableMapGoals(TEAM.AXIS, false, "DEFEND_depot.*");
	 	SetAvailableMapGoals(TEAM.ALLIES, false, "ATTACK_depot.*");
	 	SetAvailableMapGoals(TEAM.ALLIES, false, "SNIPE_depot1");
	 	SetAvailableMapGoals(TEAM.ALLIES, false, "MOUNTMG42_mg42_bank");
	 	SetAvailableMapGoals(TEAM.AXIS, true, "ATTACK_tank1");
	 	SetAvailableMapGoals(TEAM.ALLIES, true, "ATTACK_tank6");
	 	SetAvailableMapGoals(TEAM.ALLIES, true, "PLANT_Tank_Barrier_1");
	 	SetAvailableMapGoals(TEAM.ALLIES, true, "BUILD_Allied_MG42_Construction");
	 	SetAvailableMapGoals(TEAM.ALLIES, true, ".*MG42_alliedconstruct1_track");
	 	SetAvailableMapGoals(TEAM.ALLIES, true, ".*MG42_mg42_bank_1");
	 	SetGoalPriority("BUILD_Command_Post", 0.9, TEAM.ALLIES);
	 	SetGoalPriority("PLANT_Command_Post", 0.79, TEAM.ALLIES);
		Util.OnTriggerPosition("MOVER_tank", "tank_turn1", 200.0, Map.Tank_Turn1);
		Util.MapDebugPrint("Tank_Stolen");
	},

	Tank_Damaged = function(trigger)
	{
		Util.SetPositionGoal("BUILD_Tank", "MOVER_tank");
		Util.MapDebugPrint("Tank_Damaged");
	},

	Tank_Repaired = function(trigger)
	{
		Util.MapDebugPrint("Tank_Repaired");
	},

	Tank_Turn1 = function(trigger)
	{
	 	SetAvailableMapGoals(TEAM.AXIS, true, "ATTACK_tank2");
	 	SetAvailableMapGoals(TEAM.AXIS, true, "SNIPE_crane");
	 	SetGoalPriority("SNIPE_crane", 0.8, TEAM.AXIS);
	 	SetAvailableMapGoals(TEAM.AXIS, true, "BUILD_Tank_Barrier_2");
	 	SetAvailableMapGoals(TEAM.ALLIES, true, "PLANT_Tank_Barrier_2");
		Util.OnTriggerPosition("MOVER_tank", "tank_barrier1", 200.0, Map.Tank_Barrier1);
	 	Util.MapDebugPrint("Tank_Turn1");
	},

	Tank_Barrier1 = function(trigger)
	{
	 	SetGoalPriority("PLANT_Tank_Barrier_1", 0.95, TEAM.ALLIES);
		Util.OnTriggerPosition("MOVER_tank", "tank_turn2", 200.0, Map.Tank_Turn2);
	 	Util.MapDebugPrint("Tank_Barrier1");
	},

	Tank_Turn2 = function(trigger)
	{
		Map.phase = 15;
		Map.SetSpawn();
	 	SetAvailableMapGoals(TEAM.AXIS, false, "ATTACK_tank1");
	 	SetAvailableMapGoals(TEAM.ALLIES, false, ".*MG42_mg42_bank_1");
	 	SetAvailableMapGoals(TEAM.AXIS, true, "PLANT_Right_bridge");
	 	SetGoalPriority("BUILD_Tank", 0.91, TEAM.ALLIES);
		Util.OnTriggerPosition("MOVER_tank", "tank_turn3", 200.0, Map.Tank_Turn3);
	 	Util.MapDebugPrint("Tank_Turn2");
	},

	Tank_Turn3 = function(trigger)
	{
	 	SetAvailableMapGoals(TEAM.AXIS, false, "ATTACK_tank2");
	 	SetAvailableMapGoals(TEAM.ALLIES, false, "ATTACK_tank6");
	 	SetAvailableMapGoals(TEAM.AXIS, true, "ATTACK_tank3");
	 	SetAvailableMapGoals(TEAM.ALLIES, true, "ATTACK_tank7");
	 	SetGoalPriority("SNIPE_crane", 0.7, TEAM.AXIS);
	 	SetGoalPriority("PLANT_Side_Door__bunker", 0.9, TEAM.ALLIES);
		Util.OnTriggerPosition("MOVER_tank", "tank_turn4", 200.0, Map.Tank_Turn4);
	 	Util.MapDebugPrint("Tank_Turn3");
	},

	Tank_Turn4 = function(trigger)
	{
	 	SetAvailableMapGoals(TEAM.AXIS, true, "ATTACK_tank4");
		Util.OnTriggerPosition("MOVER_tank", "tank_barrier2", 200.0, Map.Tank_Barrier2);
	 	Util.MapDebugPrint("Tank_Turn4");
	},

	Tank_Barrier2 = function(trigger)
	{
		Map.phase = 20;
		Map.SetSpawn();
	 	SetGoalPriority("PLANT_Tank_Barrier_2", 0.95, TEAM.ALLIES);
	 	SetAvailableMapGoals(TEAM.AXIS, true, "ATTACK_tank5");
		Util.OnTriggerPosition("MOVER_tank", "tank_turn5", 250.0, Map.Tank_Turn5);
	 	Util.MapDebugPrint("Tank_Barrier2");
	},

	Tank_Turn5 = function(trigger)
	{
	 	SetAvailableMapGoals(TEAM.AXIS, false, "ATTACK_tank3");
	 	SetAvailableMapGoals(TEAM.ALLIES, true, "BUILD_Right_bridge");
	 	SetGoalPriority("PLANT_Tank_Barrier_2", 0.92, TEAM.ALLIES);
	 	Util.MapDebugPrint("Tank_Turn5");
	},

	Right_bridge_Built = function(trigger)
	{
		Wp.SetWaypointFlag("bridge", "closed", false);
	 	ETUtil.EnableGoal("ROUTE_bridge");
		Util.MapDebugPrint("Right_bridge_Built");
	},

	Right_bridge_Destroyed = function(trigger)
	{
		Wp.SetWaypointFlag("bridge", "closed", true);
	 	ETUtil.DisableGoal("ROUTE_bridge");
		Util.MapDebugPrint("Right_bridge_Destroyed");
	},

	Mosque_Accessible = function(trigger)
	{
		Map.phase = 30;
	 	SetAvailableMapGoals(TEAM.AXIS, false, "ATTACK_tank.*");
	 	SetAvailableMapGoals(TEAM.ALLIES, false, "ATTACK_tank7");
	 	SetAvailableMapGoals(TEAM.AXIS, true, "DEFEND_mosque.*");
	 	SetAvailableMapGoals(TEAM.AXIS, true, "DEFEND_boat.*");
	 	SetAvailableMapGoals(TEAM.AXIS, true, "BUILD_Boat_Barrier_.*");
	 	SetAvailableMapGoals(TEAM.ALLIES, true, "FLAG_Gold_Bars");
	 	SetAvailableMapGoals(TEAM.ALLIES, true, "CAPPOINT_boat");
	 	Map.EscortVehicle.Tank.Enabled = false;
		Util.MapDebugPrint("Mosque_Accessible");
	},

	SetBoat = function()
	{
	 	Map.EscortVehicle.Boat.Enabled = Map.phase>=40 && Map.boatBarrier1Destroyed;
	},

	Escort_Boat = function(trigger)
	{
		Map.phase = 40;
		Map.SetSpawn();
		Map.SetBoat();
		Util.SetMaxUsersInProgress(1, "CHECKPOINT_rien_du_tout");
		Util.SetMaxUsersInProgress(5, "CHECKPOINT_rien_du_tout_1");
	 	SetAvailableMapGoals(TEAM.AXIS, false, "DEFEND_mosque.*");
	 	SetAvailableMapGoals(TEAM.AXIS, false, "PLANT_Right_bridge");
	 	SetAvailableMapGoals(TEAM.ALLIES, false, "FLAG_Gold_Bars");
	 	SetAvailableMapGoals(TEAM.ALLIES, false, "CAPPOINT_boat");
	 	SetAvailableMapGoals(TEAM.ALLIES, false, "BUILD_Tank");
	 	SetAvailableMapGoals(TEAM.ALLIES, false, "MOVER_tank");
	 	SetAvailableMapGoals(TEAM.AXIS, true, "DEFEND_port.*");
	 	SetAvailableMapGoals(TEAM.AXIS, true, "SNIPE_port.*");
	 	SetAvailableMapGoals(TEAM.ALLIES, true, "BUILD_Boat");
	 	SetAvailableMapGoals(TEAM.ALLIES, true, "MOVER_truck");
	 	SetAvailableMapGoals(TEAM.ALLIES, true, "ATTACK_port.*");
	 	SetAvailableMapGoals(TEAM.ALLIES, true, "PLANT_Boat_Barrier_1");
	 	SetAvailableMapGoals(TEAM.ALLIES, true, "PLANT_Boat_Barrier_2");
		Util.OnTriggerPosition("MOVER_truck", "boat_barrier1", 200.0, Map.Boat_Past_Barrier1);
		Util.MapDebugPrint("Escort_Boat");
	},

	Boat_Barrier1_Constructed = function(trigger)
	{
		Map.boatBarrier1Destroyed = false;
		Map.SetBoat();
		Util.MapDebugPrint("Boat_Barrier1_Constructed");
	},

	Boat_Barrier1_Destroyed = function(trigger)
	{
		Map.boatBarrier1Destroyed = true;
		Map.SetBoat();
		Util.MapDebugPrint("Boat_Barrier1_Destroyed");
	},

	Boat_Past_Barrier1 = function(trigger)
	{
	 	SetAvailableMapGoals(TEAM.AXIS, false, "BUILD_Boat_Barrier_1");
		Util.OnTriggerPosition("MOVER_truck", "boat_barrier2", 200.0, Map.Boat_Barrier2);
		Util.MapDebugPrint("Boat_Past_Barrier1");
	},

	Boat_Barrier2 = function(trigger)
	{
	 	SetGoalPriority("PLANT_Boat_Barrier_2", 0.95, TEAM.ALLIES);
		Util.MapDebugPrint("Boat_Barrier2");
	},

	Boat_Damaged = function(trigger)
	{
		Util.SetPositionGoal("BUILD_Boat", "MOVER_truck");
		Util.SetGoalOffset(0, -30, 35, "BUILD_Boat");
		Util.MapDebugPrint("Boat_Damaged");
	},
};

global OnMapLoad = function()
{
	OnTrigger("allies_capture_rien_du_tout", Map.Bar_Captured);
	OnTrigger("axis_neutralized_rien_du_tout", Map.Bar_Reclaimed);
	OnTrigger("axis_capture_rien_du_tout", Map.Port_Captured);
	OnTrigger("allies_neutralized_rien_du_tout", Map.Port_Reclaimed);
	OnTrigger("Allies have blown the tanks bunker door!", Map.Tank_Depot_Door_Destroyed);
	OnTrigger("Allied team has stolen the Tank!", Map.Tank_Stolen);
	OnTrigger("The Tank has been damaged!", Map.Tank_Damaged);
	OnTrigger("The Tank has been repaired!", Map.Tank_Repaired);
	OnTrigger("Allies have blown the Side bunker door!", Map.Side_Door__bunker_Destroyed);
	OnTrigger("Tank Barrier #1 has been constructed.", Map.Tank_Barrier1_Constructed);
	OnTrigger("Tank Barrier #1 has been destroyed.", Map.Tank_Barrier1_Destroyed);
	OnTrigger("Right Bridge moskee has been constructed.", Map.Right_bridge_Built);
	OnTrigger("Right Bridge moskee has been destroyed.", Map.Right_bridge_Destroyed);
	OnTrigger("Allied team has destroyed the Moskee Doors Whit the Tank!", Map.Mosque_Accessible);
	OnTrigger("Allied team is escaping with the Gold Crates and the Boat and try to re", Map.Escort_Boat);
	OnTrigger("The Boat has been damaged!", Map.Boat_Damaged);
	OnTrigger("Truck Barrier #1 has been constructed.", Map.Boat_Barrier1_Constructed);
	OnTrigger("Boat Barrier #1 has been constructed.", Map.Boat_Barrier1_Constructed);
	OnTrigger("Boat Barrier #1 has been destroyed.", Map.Boat_Barrier1_Destroyed);

	Util.DisableGoal(".*", true);
 	Util.DisableGoal("ROUTE_side_door");
	Util.EnableGoal(".*Command_Post");
	Util.EnableGoal("AMMO.*");
	Util.EnableGoal("HEALTH.*");
 	//SetAvailableMapGoals(TEAM.ALLIES, false, ".*lighthouse.*"); // cs: no goals match this regex

 	SetAvailableMapGoals(TEAM.AXIS, true, "PLANT_Allied_MG42_Construction");
 	SetAvailableMapGoals(TEAM.AXIS, true, "BUILD_Tank_Barrier_1");
 	SetAvailableMapGoals(TEAM.AXIS, true, ".*MG42_mg42_bank_1");
 	SetAvailableMapGoals(TEAM.AXIS, true, "CHECKPOINT_rien_du_tout_1"); //port roof
 	SetAvailableMapGoals(TEAM.AXIS, true, "DEFEND_beach.*");
 	SetAvailableMapGoals(TEAM.AXIS, true, "DEFEND_depot.*");
 	SetAvailableMapGoals(TEAM.AXIS, true, "DEFEND_side_door.*");
 	SetAvailableMapGoals(TEAM.ALLIES, true, "ATTACK_depot.*");
 	SetAvailableMapGoals(TEAM.ALLIES, true, "SNIPE_depot1");
 	SetAvailableMapGoals(TEAM.ALLIES, true, "PLANT_Tank_Depot_Door");
 	SetAvailableMapGoals(TEAM.ALLIES, true, ".*MG42_mg42_bank");
 	SetAvailableMapGoals(TEAM.ALLIES, true, "CHECKPOINT_rien_du_tout"); //bar

 	SetGoalPriority("DEFEND_beach.*", 0.51, TEAM.AXIS);
 	SetGoalPriority("BUILD_Tank_Barrier_1", 0.95, TEAM.AXIS);
 	SetGoalPriority("BUILD_Tank_Barrier_2", 0.92, TEAM.AXIS);
 	SetGoalPriority("BUILD_Boat_Barrier_1", 0.95, TEAM.AXIS);
 	SetGoalPriority("BUILD_Boat_Barrier_2", 0.92, TEAM.AXIS);
 	SetGoalPriority("PLANT_Boat_Barrier_1", 0.95, TEAM.ALLIES);
 	SetGoalPriority("BUILD_Command_Post", 0.8, TEAM.ALLIES);
 	SetGoalPriority("PLANT_Command_Post", 0.7, TEAM.ALLIES);


	foreach (goal in { ".*MG42.*", "SNIPE.*", "PLANT_Command_Post" }){
		Util.SetMaxUsersInProgress(1, goal);
	}
	Util.SetMaxUsersInProgress(3, ".*MG42_alliedconstruct1_track");
	Util.SetMaxUsersInProgress(2, "CHECKPOINT_rien_du_tout");
	Util.SetMaxUsersInProgress(1, "CHECKPOINT_rien_du_tout_1");

 	SetMapGoalProperties("DEFEND_.*", {mincamptime=70, maxcamptime=300} );
 	SetMapGoalProperties("MOUNTMG42.*", {mincamptime=70, maxcamptime=300} );
 	SetMapGoalProperties("MOUNTMG42_alliedconstruct1_track", {mincamptime=999, maxcamptime=999} );
 	SetMapGoalProperties("SNIPE_.*", {mincamptime=60, maxcamptime=250} );
	Wp.SetWaypointFlag("bridge", "closed", true);

	MapRoutes =
	{
		ATTACK_depot2 = {
			ROUTE_helicopter_spawn = {
				ROUTE_beach_left = { ROUTE_town_east_north = {}},
				ROUTE_beach_right = { Weight = 3,
					ROUTE_town_west_south = {
						ROUTE_town_south = {},
						ROUTE_town_yard = {},
					},
				},
			},
			ROUTE_bar_spawn = {
				ROUTE_side_door = {},
				ROUTE_town_east_north = { Weight = 3, },
			}
		},
		MOUNTMG42_mg42_bank = {
			ROUTE_helicopter_spawn = {
				ROUTE_town_west_south= { ROUTE_town_south = {}},
			}
		},
		CHECKPOINT_rien_du_tout = {
			ROUTE_bunker_spawn = {
				ROUTE_side_door_axis = {},
				ROUTE_town_east_north = {},
			},
		},
		BUILD_Right_bridge = {
			ROUTE_bar_spawn = {
				ROUTE_side2 = {},
				ROUTE_side3 = {},
			}
		},
		FLAG_Gold_Bars = {
			ROUTE_bar_spawn = {
				ROUTE_side_door = { Weight = 9,
					ROUTE_side1 = {},
					ROUTE_side2 = {},
					ROUTE_side3 = {},
					ROUTE_side4 = {},
					ROUTE_sewer = {},
				},
				ROUTE_town_east_north = {},
			}
		},
		CAPPOINT_boat = {
			ROUTE_gold = {
				ROUTE_port_entrance = {},
				ROUTE_port_west = { Weight = 2 },
			}
		},
		PLANT_Boat_Barrier_1 = {
			ROUTE_bar_spawn = {
				ROUTE_port_entrance = {},
				ROUTE_port_west = { Weight = 2 },
			},
		},
		DEFEND_mosque1 = {
			ROUTE_axis_spawn = {
				ROUTE_axis_north = { Weight = 3 },
				ROUTE_axis_south = { Weight = 3 },
				ROUTE_tunnel = {},
			}
		},
	};
	MapRoutes.PLANT_Tank_Depot_Door = MapRoutes.ATTACK_depot2;
	MapRoutes.ATTACK_depot3 = MapRoutes.ATTACK_depot2;
	MapRoutes.BUILD_Command_Post = MapRoutes.ATTACK_depot2;
	MapRoutes.DEFEND_mosque2 = MapRoutes.DEFEND_mosque1;
	MapRoutes.DEFEND_mosque3 = MapRoutes.DEFEND_mosque1;
	MapRoutes.DEFEND_mosque4 = MapRoutes.DEFEND_mosque1;
	MapRoutes.PLANT_Right_bridge = MapRoutes.DEFEND_mosque1;
	Util.Routes(MapRoutes);
};

global OnBotJoin = function( bot )
{
	bot.TargetBreakableDist = 200.0;
	bot.MaxViewDistance = 3600;
	Map.SetSpawn1(bot);

	foreach (ent in {731, 732, 735, 736}){
		bot.IgnoreTarget(ent, 9999);
	}
};
