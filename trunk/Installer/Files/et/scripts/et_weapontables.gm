// weapon selection functions and tables

///////////////////////////////////////////////////////////////////////////////////////////
// TABLES
///////////////////////////////////////////////////////////////////////////////////////////

global WeaponTable = {};

global PrimaryWeaponChoices = {};
PrimaryWeaponChoices[TEAM.AXIS] = {};
PrimaryWeaponChoices[TEAM.ALLIES] = {};

////////////////////
// soldiers weapons
////////////////////
tbl = {};
tbl[WEAPON.PANZERFAUST] = true;
tbl[WEAPON.MOBILE_MG42] = true;
tbl[WEAPON.FLAMETHROWER] = true;
tbl[WEAPON.MORTAR] = true;
isNq = (GetModName() == "noquarter");
isEtnam = GetModName() == "etpub" && GetCvar("g_etnamFlags") > 0;
if (isNq) {
	if (GetCvar("jp_insanity") & 256) {
		// if allowed, add venom to the list.
		tbl[ WEAPON.VENOM ] = true;
	}
	tbl[WEAPON.STG44] = true;
}else{
	if (isEtnam) {
		tbl[WEAPON.M79] = true;
		if (!WEAPON.ITHACA) { WEAPON.ITHACA = 102; }
		tbl[WEAPON.ITHACA] = false;
	}
	tbl[WEAPON.MP40] = false;
}
PrimaryWeaponChoices[TEAM.AXIS][CLASS.SOLDIER] = tbl;

tbl = {};
tbl[WEAPON.MOBILE_MG42] = true;
tbl[WEAPON.FLAMETHROWER] = true;
tbl[WEAPON.MORTAR] = true;
if (isNq) {
	if (GetCvar("jp_insanity") & 256) {
		// if allowed, add venom to the list.
		tbl[ WEAPON.VENOM ] = true;
	}
	WeaponTable.AlliedPanzer = WEAPON.BAZOOKA;
	tbl[WEAPON.BAR] = true;
}else{
	if (isEtnam) {
		WeaponTable.AlliedPanzer = WEAPON.LAW;
		tbl[WEAPON.M79] = true;
		tbl[WEAPON.ITHACA] = false;
	}else{
		WeaponTable.AlliedPanzer = WEAPON.PANZERFAUST;
	}
	tbl[WEAPON.THOMPSON] = false;
}
tbl[WeaponTable.AlliedPanzer] = true;
PrimaryWeaponChoices[TEAM.ALLIES][CLASS.SOLDIER] = tbl;

if (GetModName() == "Jaymod") {
	PrimaryWeaponChoices[TEAM.AXIS][CLASS.SOLDIER][WEAPON.M97] = false;
	PrimaryWeaponChoices[TEAM.ALLIES][CLASS.SOLDIER][WEAPON.M97] = false;
}

if (GetModName() == "silEnT") {
	PrimaryWeaponChoices[TEAM.AXIS][CLASS.SOLDIER][/*WEAPON.PPSH*/ 101] = false;
	PrimaryWeaponChoices[TEAM.ALLIES][CLASS.SOLDIER][/*WEAPON.PPSH*/ 101] = false;
}

/////////////////////
// covertops weapons
/////////////////////
tbl = {};
tbl[WEAPON.K43] = true;
if (isEtnam) {
	tbl[WEAPON.SAK47] = true;
	tbl[WEAPON.RPD] = true;
}else{
	tbl[WEAPON.FG42] = true;
}
if (isNq) {
	tbl[WEAPON.MP34] = true;
}else{
	tbl[WEAPON.STEN] = true;
}
PrimaryWeaponChoices[TEAM.AXIS][CLASS.COVERTOPS] = tbl;

tbl = {};
tbl[WEAPON.GARAND] = true;
tbl[WEAPON.STEN] = true;
if (isEtnam) {
	tbl[WEAPON.M3A1] = true;
}
if (isNq) {
	tbl[WEAPON.BAR] = true;
}else{
	tbl[WEAPON.FG42] = true;
}
PrimaryWeaponChoices[TEAM.ALLIES][CLASS.COVERTOPS] = tbl;



///////////////////////////////////////////////////////////////////////////////////////////
// FUNCTIONS
///////////////////////////////////////////////////////////////////////////////////////////

WeaponTable.SetWeaponTeamAvailability = function(team, class, weapon, enable)
{
	classTable = PrimaryWeaponChoices[team][class];
	if (!classTable) {
		Util.MapDebugPrint("WeaponTable.SetWeaponAvailabilty: No weapon table for class " + Util.ClassName(class), true);
		return;
	}

	if ( typeName(weapon) == "table" ) {
		foreach ( wpn in weapon ) {
			this.SetWeaponTeamAvailability(team, class, wpn, enable);
		}
		return;
	}

	if (weapon == WEAPON.PANZERFAUST && team == TEAM.ALLIES) {
		weapon = WeaponTable.AlliedPanzer;
	}
	if (GetModName() == "noquarter") {
		if (team == TEAM.AXIS && weapon == WEAPON.STEN) {
			weapon = WEAPON.MP34;
		}
	}

	if (typeId(classTable[weapon]) !=0 ) { // enable only weapons which are permitted for this team and class
		classTable[weapon] = enable;
	}
};

/*
	WeaponTable.SetWeaponAvailability( CLASS.SOLDIER, WEAPON.FLAMETHROWER, true );
	WeaponTable.SetWeaponAvailability( CLASS.SOLDIER, {WEAPON.MORTAR, WEAPON.MOBILE_MG42}, false);
*/
WeaponTable.SetWeaponAvailability = function(class, weapon, enable)
{
	WeaponTable.SetWeaponTeamAvailability(TEAM.AXIS, class, weapon, enable);
	WeaponTable.SetWeaponTeamAvailability(TEAM.ALLIES, class, weapon, enable);
};

WeaponTable.GetPrimaryWeapons = function(team, class)
{
	return PrimaryWeaponChoices[team][class];
};

// for debug
WeaponTable.ListWeaponAvailability = function()
{
	print("\n//////////// Weapon Availability ////////////");
	foreach ( team and tbl in PrimaryWeaponChoices )
	{
		print(format("\n Team: %s", Util.TeamName(team)));
		foreach ( id and class in tbl) {
			print(format("\n------ %s ------", Util.ClassName(id)));

			foreach ( w and e in class ) {
				print(format("%s = %d", Util.WeaponName(w), e));
			}
		}
	}
};
