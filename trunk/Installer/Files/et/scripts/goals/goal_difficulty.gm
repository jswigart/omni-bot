// This script sets difficulty settings for the bots
this.Name = "Difficulty";
this.Parent = "LowLevel";
this.Debug = false;
this.AutoAdd = true;
this.AlwaysRecieveEvents = true;

// initialize defaults in case the entry isn't in omni-bot.cfg
this.CurrentDifficulty = 4;
this.AdjustAim = true;

this.CurrentDifficulty = ConfigGet("Difficulty","CurrentDifficulty", this.CurrentDifficulty);
this.AdjustAim = ConfigGet("Difficulty","AdjustAim", this.AdjustAim);
this.UseWeaponSpecificTolerances = true;

this.Initialize = function()
{
	// run it once at start
	this.FirstRun = true;
};

this.GetPriority = function()
{
	yield();
	if ( this.FirstRun ) {
		this.FirstRun = false;
		this.Priority = 1.0;
	}
	block(0);
};

this.Update = function()
{
	// if a bot profile sets a ProfileSkill, this will run only once per round.
	if ( !this.Bot.LockSkill && this.CurrentDifficulty != this.Bot.aimskill )
	{
		if (!this.Bot.ProfileSkill)
		{
			this.Bot.aimskill = this.CurrentDifficulty;
		}
		else
		{
			this.Bot.aimskill = this.Bot.ProfileSkill;
			this.Bot.LockSkill = true;
		}

		if( this.Bot.aimskill < 0 )
		{
			n = tableCount(this.difficulties);
			r = RandInt(0, n-1);
			useDiff = r;
		}
		else
		{
			useDiff = this.Bot.aimskill;
		}

		if ( !this.difficulties[useDiff] )
		{
			useDiff = 4; // use original default
		}

		this.Bot.FieldOfView = this.difficulties[useDiff].FieldOfView;
		this.Bot.ReactionTime = this.difficulties[useDiff].ReactionTime;
		this.Bot.MemorySpan = this.difficulties[useDiff].MemorySpan;
		this.Bot.AimPersistance = this.difficulties[useDiff].AimPersistance;
		this.Bot.MaxTurnSpeed = this.difficulties[useDiff].MaxTurnSpeed;
		this.Bot.AimStiffness = this.difficulties[useDiff].AimStiffness;
		this.Bot.AimDamping = this.difficulties[useDiff].AimDamping;
		this.Bot.AimTolerance = this.difficulties[useDiff].AimTolerance;
		this.Bot.DefaultTolerance = this.Bot.AimTolerance;
		this.Bot.WeaponTolerances = this.difficulties[useDiff].WeaponTolerances;

		// don't accumulate percentages every difficulty change
		if(!this.Bot.OriginalMaxView) { this.Bot.OriginalMaxView = this.Bot.MaxViewDistance; }
		this.Bot.MaxViewDistance = this.Bot.OriginalMaxView * (this.difficulties[useDiff].MaxViewPercent * .01);

		if ( this.Debug ) {
			print( "--- " +this.Bot.Name + " Aim Properties ---" );
			print( "Difficulty: " + useDiff );
			print( "FieldOfView: " + this.Bot.FieldOfView );
			print( "ReactionTime: " + this.Bot.ReactionTime );
			print( "MemorySpan: " + this.Bot.MemorySpan );
			print( "AimPersistance: " + this.Bot.AimPersistance );
			print( "MaxTurnSpeed: " + this.Bot.MaxTurnSpeed );
			print( "AimStiffness: " + this.Bot.AimStiffness );
			print( "AimDamping: " + this.Bot.AimDamping );
			print( "AimTolerance: " + this.Bot.AimTolerance );
			print( "MaxViewDistance: " + this.Bot.MaxViewDistance );
			print( "------------ " );
		}

		if ( this.AdjustAim ) {
			this.AdjustWeaponProperties(this.Bot, useDiff);
		}

		this.SetAimToleranceForWeapon(this.Bot.GetCurrentWeapon());
	}

	this.Finished();
};

this.difficulties =
{
	// Difficulty 0
	{
		FieldOfView = 90.0,
		ReactionTime = 2.0,
		MemorySpan = 1.5,
		AimPersistance = 1.0,
		MaxTurnSpeed = 720.0,
		AimStiffness = 60.0,
		AimDamping = 20.0,
		AimTolerance = 125,
		AimErrorX = 25,
		AimErrorY = 25,
		AimOffsetX = 0,
		AimOffsetY = 0,
		AimOffsetZ = -15,
		MaxViewPercent = 65,
		WeaponTolerances =
		{
			VENOM = 128,
		},
	},
	// Difficulty 1
	{
		FieldOfView = 90.0,
		ReactionTime = 2.0,
		MemorySpan = 1.5,
		AimPersistance = 1.0,
		MaxTurnSpeed = 720.0,
		AimStiffness = 60.0,
		AimDamping = 20.0,
		AimTolerance = 125,
		AimErrorX = 25,
		AimErrorY = 25,
		AimOffsetX = 0,
		AimOffsetY = 0,
		AimOffsetZ = -15,
		MaxViewPercent = 65,
		SnipeDelay = 2.0,
		WeaponTolerances =
		{
			VENOM = 128,
		},
	},
	// Difficulty 2
	{
		FieldOfView = 100.0,
		ReactionTime = 2.0,
		MemorySpan = 2.0,
		AimPersistance = 1.0,
		MaxTurnSpeed = 720.0,
		AimStiffness = 50.0,
		AimDamping = 15.0,
		AimTolerance = 110,
		AimErrorX = 20,
		AimErrorY = 20,
		AimOffsetX = 0,
		AimOffsetY = 0,
		AimOffsetZ = -15,
		MaxViewPercent = 75,
		SnipeDelay = 1.6,
		WeaponTolerances =
		{
			VENOM = 128,
		},
	},
	// Difficulty 3
	{
		FieldOfView = 105.0,
		ReactionTime = 1.0,
		MemorySpan = 2.5,
		AimPersistance = 2.0,
		MaxTurnSpeed = 720.0,
		AimStiffness = 60.0,
		AimDamping = 15.0,
		AimTolerance = 100,
		AimErrorX = 15,
		AimErrorY = 15,
		AimOffsetX = 0,
		AimOffsetY = 0,
		AimOffsetZ = -12,
		MaxViewPercent = 85,
		SnipeDelay = 0.8,
		WeaponTolerances =
		{
			VENOM = 128,
		},
	},
	// Difficulty 4
	{
		FieldOfView = 110.0,
		ReactionTime = 0.75,
		MemorySpan = 3.0,
		AimPersistance = 2.0,
		MaxTurnSpeed = 720.0,
		AimStiffness = 65.0,
		AimDamping = 15.0,
		AimTolerance = 90,
		AimErrorX = 10,
		AimErrorY = 10,
		AimOffsetX = 0,
		AimOffsetY = 0,
		AimOffsetZ = -10,
		MaxViewPercent = 90,
		SnipeDelay = 0.4,
		WeaponTolerances =
		{
			VENOM = 128,
		},
	},
	// Difficulty 5
	{
		FieldOfView = 120.0,
		ReactionTime = 0.5,
		MemorySpan = 3.0,
		AimPersistance = 2.0,
		MaxTurnSpeed = 720.0,
		AimStiffness = 75.0,
		AimDamping = 10.0,
		AimTolerance = 64,
		AimErrorX = 5,
		AimErrorY = 5,
		AimOffsetX = 0,
		AimOffsetY = 0,
		AimOffsetZ = -5,
		MaxViewPercent = 100,
		SnipeDelay = 0.2,
		WeaponTolerances =
		{
			VENOM = 128,
		},
	},
	// Difficulty 6
	{
		FieldOfView = 180.0,
		ReactionTime = 0.0,
		MemorySpan = 4.0,
		AimPersistance = 2.0,
		MaxTurnSpeed = 720.0,
		AimStiffness = 75.0,
		AimDamping = 10.0,
		AimTolerance = 64,
		AimErrorX = 0,
		AimErrorY = 0,
		AimOffsetX = 0,
		AimOffsetY = 0,
		AimOffsetZ = 0,
		MaxViewPercent = 110,
		SnipeDelay = 0.1,
		WeaponTolerances =
		{
			VENOM = 128,
		},
	},
};

this.AdjustWeaponProperties = function(bot, skill)
{
	AdjustableWeapons =
	{
		WEAPON.THOMPSON,
		WEAPON.MP40,
		WEAPON.LUGER,
		WEAPON.LUGER_SILENCED,
		WEAPON.COLT,
		WEAPON.COLT_SILENCED,
		WEAPON.MOUNTABLE_MG42,
		WEAPON.STEN,
		WEAPON.COLT_AKIMBO,
		WEAPON.LUGER_AKIMBO,
		WEAPON.COLT_AKIMBO_SILENCED,
		WEAPON.LUGER_AKIMBO_SILENCED,
		WEAPON.FG42,
		WEAPON.FG42_SCOPE,
		WEAPON.GPG40,
		WEAPON.M7,
		WEAPON.K43,
		WEAPON.K43_SCOPE,
		WEAPON.GARAND,
		WEAPON.GARAND_SCOPE,
	};

	SnipeWeapons =
	{
		WEAPON.GARAND_SCOPE,
		WEAPON.K43_SCOPE,
	};

	foreach ( id and weap in AdjustableWeapons )
	{
		w = bot.GetWeapon(weap);
		if ( w )
		{
			w.PrimaryFire.MaxAimError = Vector2(this.difficulties[skill].AimErrorX, this.difficulties[skill].AimErrorY);
			w.PrimaryFire.AimOffset = Vector3(this.difficulties[skill].AimOffsetX, this.difficulties[skill].AimOffsetY, this.difficulties[skill].AimOffsetZ);
		}
	}
};

///////////////////////////////////////////////////////////////////////////////

this.Commands["difficulty"] =
{
	Func = function( _params )
	{
		if(typeId(_params[0]) != 0)
		{
			diff = Clamp( ToInt(_params[0], 4), -1, tableCount(this.difficulties) );

			ConfigSet("Difficulty","CurrentDifficulty", diff);
			this.CurrentDifficulty = diff;
			print("Difficulty Changed", diff, " for ", this.Bot.Name);
			this.Priority = 1.0; // run update
		}

		diff = ConfigGet("Difficulty","CurrentDifficulty", diff);
		print("Difficulty:", diff);
	},

	Help =
	{
		"Sets the bots difficulty (-1...6), -1 is random, default 4",
	},
};

///////////////////////////////////////////////////////////////////////////////

this.SetAimToleranceForWeapon = function(weaponId)
{
	if (this.FirstRun || !this.UseWeaponSpecificTolerances ) { return; }
	yield(); yield(); // need to make sure DefaultTolerance is set.
	if (!this.Bot) { return; }

	weaponName = Util.WeaponName(weaponId);
	if ( this.Bot.WeaponTolerances && this.Bot.WeaponTolerances[weaponName] ) {
		if ( this.Debug ) {
			this.Bot.Say("changing aim tolerance to " + this.Bot.WeaponTolerances[weaponName] + " for " + weaponName);
		}
		this.Bot.AimTolerance = this.Bot.WeaponTolerances[weaponName];
	} else {
		if ( this.Debug ) {
			this.Bot.Say(format("using default tolerance for %s",weaponName));
		}
		this.Bot.AimTolerance = this.Bot.DefaultTolerance;
	}
};

this.Events[EVENT.WEAPON_CHANGE] = this.SetAimToleranceForWeapon;
