if ( Stats == null ) {
	global Stats = table();

	///////////////////////////////////////////////////////////////////////////////

	Stats.DumpStats = function()
	{

		db = sqlite();
		db.Open( "stats.db" );

		if ( db.Query( "CREATE TABLE game(
						gameid INTEGER PRIMARY KEY,
						map TEXT,
						name TEXT,
						kills INT,
						deaths INT,
						kill_ratio FLOAT,
						shots INT,
						hits INT,
						accuracy FLOAT,
						headshots INT,
						headshot_percent FLOAT,
						gibs INT,
						health_given INT,
						ammo_given INT,
						timestamp DATE
						);") != 0
			 ) {
			print( "Game Table Created" );
		}

		if ( db.Query( "CREATE TRIGGER insert_timestamp AFTER INSERT ON game
						BEGIN
						UPDATE game SET timestamp = DATETIME('NOW')
						WHERE rowid = new.rowid;
						END;") != 0
			 ) {
			print( "Timestamp Trigger Created" );
		}

		for ( i = 0; i < 64; i += 1 )
		{
			ent = GetGameEntityFromId( i );

			if ( !ent ) {
				continue;
			}

			map = "'" + GetMapName() + "',";
			name = "'" + GetEntName( i ) + "',";
			kills = "'" + GetEntityStat( ent, "kills" ) + "',";
			deaths = "'" + GetEntityStat( ent, "deaths" ) + "',";
			kr = "'" + GetEntityStat( ent, "killratio" ) + "',";
			shots = "'" + GetEntityStat( ent, "shots" ) + "',";
			hits = "'" + GetEntityStat( ent, "hits" ) + "',";
			acc = "'" + GetEntityStat( ent, "accuracy" ) + "',";
			hs = "'" + GetEntityStat( ent, "headshots" ) + "',";
			hspercent = "'" + GetEntityStat( ent, "hspercent" ) + "',";
			gibs = "'" + GetEntityStat( ent, "gibs" ) + "',";
			health = "'" + GetEntityStat( ent, "health" ) + "',";
			ammo = "'" + GetEntityStat( ent, "ammo" ) + "'";

			game = "INSERT INTO game (map,name,kills,deaths,kill_ratio,shots,hits,accuracy,
									headshots,headshot_percent,gibs,health_given,
									ammo_given) values ("
				   + map
				   + name
				   + kills
				   + deaths
				   + kr
				   + shots
				   + hits
				   + acc
				   + hs
				   + hspercent
				   + gibs
				   + health
				   + ammo
				   + ");";

			db.Query( game );
		}

		db.Close();
	};

	Stats.GetMapRecord = function( stat, map )
	{
		if ( !map ) {
			map = GetMapName();
		}

		db = sqlite();
		db.Open( "stats.db" );

		s = "SELECT * FROM game WHERE " + stat + "=" +
			"(SELECT MAX(" + stat + ") FROM game WHERE map='" + map + "');";

		record = db.Query( s );

		if ( record && tableCount( record ) > 0 ) {
			print( record[0].name, "^2has the highest", stat, "for a single round of", map, "with", record[0][stat] );
		} else
		{
			print( "No Records" );
		}

		db.Close();
	};
}

///////////////////////////////////////////////////////////////////

Commands["mostkills"] =
{
	Func = function( _params )
	{
		Stats.GetMapRecord( "kills" );
	},

	Help =
	{
		"list record holder for most kills in a round",
	},
};